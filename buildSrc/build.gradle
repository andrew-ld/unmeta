apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'java-gradle-plugin'
apply plugin: 'kotlin'

def getGroup = { ->
    return 'com.github.oliverjonas'
}

def getArtifact = { ->
    return 'unmeta'
}

def getVersion = { ->
    return '1.0.2'
}

buildscript {
    ext.kotlin_version = '1.4.21'
    ext.gradle_version = '3.5.3'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        //noinspection DifferentKotlinGradleVersion
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.android.tools.build:gradle:$gradle_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
    }
}

repositories {
    google()
    jcenter()
}

gradlePlugin {
    plugins {
        create(getGroup() + '.' + getArtifact()) {
            id = getGroup() + '.' + getArtifact()
            implementationClass = getGroup() + '.' + getArtifact() + '.UnmetaPlugin'
        }
    }
}

dependencies {
    // Android gradle plugin will allow us to access Android specific features
    implementation "com.android.tools.build:gradle:$gradle_version"
    implementation 'org.ow2.asm:asm:7.0'
}

publishing {
    publications {
        Publication(MavenPublication) {
            from components.java
            pom.withXml {
                asNode().dependencies.dependency.each { dep ->
                    dep.parent().remove(dep)
                }
            }
            group = getGroup()
            groupId = getGroup()
            artifactId = getArtifact()
            version = getVersion()
        }
    }
    repositories {
        jcenter()
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['Publication']
    pkg {
        repo = 'maven'
        name = 'unmeta'
        userOrg = user
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/oliver-jonas/unmeta.git'
        version {
            name = getVersion()
        }
    }
}
